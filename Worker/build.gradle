plugins {
    id 'java'
    id 'idea'
    id 'application'
    id 'org.springframework.boot' version '3.0.5'
}

apply plugin: 'io.spring.dependency-management'

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    jaxb
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'com.github.dpaukov:combinatoricslib3:3.3.0'
    implementation 'wsdl4j:wsdl4j'
    implementation 'commons-codec:commons-codec:1.15'
    jaxb (
            "org.glassfish.jaxb:jaxb-xjc",
            "jakarta.xml.bind:jakarta.xml.bind-api",
            "org.glassfish.jaxb:jaxb-runtime",
    )
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'build/generated-sources/jaxb'
        }
    }
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.xsdsDir = "src/main/resources/"
    outputs.dir sourcesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask", classpath: configurations.jaxb.asPath
            println(configurations.jaxb.asPath)
            mkdir(dir: sourcesDir)
            xjc(destdir: sourcesDir) {
                arg(value: "-wsdl")
                schema(dir: xsdsDir, includes: "*.xsd")
                produces(dir: sourcesDir, includes: "**/*.java")
            }
        }
    }
}

compileJava.dependsOn genJaxb

test {
    useJUnitPlatform()
}

mainClassName = 'org.example.Main'
